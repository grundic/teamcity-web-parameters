buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.9.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.rodm.teamcity-server'

group = 'ru.mail.teamcity'
version = '1.6.2'

repositories {
    mavenCentral()
    maven {
        url "http://repository.jetbrains.com/all"
    }
    maven {
        url "http://download.jetbrains.com/teamcity-repository"
    }
    mavenLocal()
}

ext {
    teamcityVersion = System.properties['teamcity.version'] ?: '10.0.2'
    teamcityDir = "${System.properties['user.home']}/.teamcity.d"
    java8Home = project.hasProperty('java8.home') ? property('java8.home') : '/Library/Java/JavaVirtualMachines/jdk1.8.0_65.jdk/Contents/Home'
    serverOpts = "-DTC.res.disableAll=true " +
            "-Dteamcity.development.mode=true " +
            "-Dteamcity.development.shadowCopyClasses=true " +
            "-Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=50055"
}

test {
    useTestNG()
}

dependencies {
    provided "org.jetbrains.teamcity:server-api:${teamcityVersion}"
    /* This dependency is not provided by JetBrains,
     * so you have to install it manually from TeamCity distribution
     * in order to be able to build plugin:
     *
     * mvn install:install-file -Dfile=webapps/ROOT/WEB-INF/lib/web.jar\
     * -DgroupId=org.jetbrains.teamcity -DartifactId=web -Dversion=9.1.6 -Dpackaging=jar
     */
    provided "org.jetbrains.teamcity:web:${teamcityVersion}"
    provided "org.jetbrains.teamcity:server:${teamcityVersion}"
    testRuntime "org.jetbrains.teamcity:tests-support:${teamcityVersion}"


    compile "org.apache.httpcomponents:httpclient:4.3.4"
    compile "com.fasterxml.jackson.core:jackson-core:2.2.2"
    compile "com.fasterxml.jackson.core:jackson-databind:2.2.2"
    compile "com.fasterxml.jackson.core:jackson-annotations:2.2.2"
    compile "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.4.1"
}

teamcity {
    version = teamcityVersion

    server {
        downloadsDir = "${teamcityDir}/_downloads"

        descriptor = file("teamcity-plugin.xml")
        tokens = [Version: project.version]


        environments {
            teamcity81 {
                version = '8.1.5'
                homeDir = file("$teamcityDir/TeamCity-${version}")
                dataDir = file("$teamcityDir/data/8.1")
                javaHome = file(java8Home)
                serverOptions = serverOpts
            }

            teamcity91 {
                version = '9.1.6'
                homeDir = file("$teamcityDir/TeamCity-${version}")
                dataDir = file("$teamcityDir/data/9.1")
                javaHome = file(java8Home)
                serverOptions = serverOpts
            }

            teamcity10 {
                version = '10.0.2'
                homeDir = file("$teamcityDir/TeamCity-${version}")
                dataDir = file("$teamcityDir/data/${version}")
                javaHome = file(java8Home)
                serverOptions = serverOpts
            }
        }
    }
}
